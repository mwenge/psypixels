\chapter{Full Source Code of the Listing} 
\lstset{style=6502Style}
If you look at Psychedelia at work for even a short time it is possible to guess at the fundamentals
of its operation. It takes the position of the cursor, paints a pattern with the cursor as its centre,
and then cycles each pixel it has painted through a series of colors.

As we look some more we might notice that the color a pixel changes to depends on what color it has already.
This becomes more noticable as we move the cursor around. If we're paying very close attention we might notice
that the number of avilable colors is fairly limited, perhaps as low as 7 or 8.

To improve our understanding any further we will need to look at the code. We might have to take this part slowly.
Although the code is very compact it might be hard to disentangle. Scanning through the 800 or so lines of assembly
we notice something relevant to our interests:

\clearpage
\begin{lstlisting}
starOneXPosArray
  .BYTE $00,$01,$01,$01,$00,$FF,$FF,$FF,$55       ;        5       
  .BYTE $00,$02,$00,$FE,$55                       ;                
  .BYTE $00,$03,$00,$FD,$55                       ;       4 4      
  .BYTE $00,$04,$00,$FC,$55                       ;        3       
  .BYTE $FF,$01,$05,$05,$01,$FF,$FB,$FB,$55       ;        2       
  .BYTE $00,$07,$00,$F9,$55                       ;        1       
  .BYTE $55                                       ;   4   000   4  
starOneYPosArray
  .BYTE $FF,$FF,$00,$01,$01,$01,$00,$FF,$55       ; 5  3210 0123  5
  .BYTE $FE,$00,$02,$00,$55                       ;   4   000   4  
  .BYTE $FD,$00,$03,$00,$55                       ;        1       
  .BYTE $FC,$00,$04,$00,$55                       ;        2       
  .BYTE $FB,$FB,$FF,$01,$05,$05,$01,$FF,$55       ;        3       
  .BYTE $F9,$00,$07,$00,$55                       ;       4 4      
  .BYTE $55                                       ;                
                                                  ;        5       

countToMatchCurrentIndex   .BYTE $01

;-------------------------------------------------------
; PutRandomByteInAccumulator
;-------------------------------------------------------
PutRandomByteInAccumulator   
randomByteAddress=$414E
        LDA $E199,X
        INC randomByteAddress
        RTS 

        BRK #$00

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.

\clearpage
\begin{lstlisting}
currentSymmetrySettingForStep   .BYTE $00
pixelXPositionArray   
        .BYTE $0F,$0E,$0D,$0C,$0B,$0A,$09,$04
        .BYTE $05,$06,$07,$08,$09,$0A,$0B,$0C
        .BYTE $0D,$0E,$0F,$10,$11,$12,$13,$14
        .BYTE $15,$16,$17,$14,$13,$12,$11,$10
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
pixelYPositionArray   
        .BYTE $0C,$0D,$0E,$0F,$0F,$0F,$0E,$04
        .BYTE $04,$04,$04,$04,$04,$04,$04,$05
        .BYTE $06,$07,$08,$09,$0A,$0B,$0C,$0D
        .BYTE $0D,$0D,$0D,$07,$09,$09,$0A,$0B
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
currentColorIndexArray   
        .BYTE $FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
        .BYTE $FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
        .BYTE $FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
        .BYTE $FF,$FF,$FF,$FF,$FF,$FF,$FF,$FF
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
initialFramesRemainingToNextPaintForStep   
        .BYTE $0C,$0C,$0C,$0C,$0C,$0C,$0C,$0C
        .BYTE $0C,$0C,$0C,$0C,$0C,$0C,$0C,$0C
        .BYTE $0C,$0C,$0C,$0C,$0C,$0C,$0C,$0C
        .BYTE $0C,$0C,$0C,$0C,$0C,$0C,$0C,$0C
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
framesRemainingToNextPaintForStep   
        .BYTE $04,$07,$01,$02,$03,$06,$07,$06
        .BYTE $0C,$02,$03,$06,$07,$01,$02,$02
        .BYTE $04,$04,$07,$01,$02,$03,$06,$07
        .BYTE $0C,$02,$03,$02,$03,$07,$01,$02
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00
        .BYTE $00,$00,$00,$00,$00,$00,$00,$00


\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.





\clearpage
\begin{lstlisting}
* = $0801
;------------------------------------------------------
; Start program at CopyCodeToRAM (SYS 2064)
; SYS 2064 ($0810)
;------------------------------------------------------
        .BYTE $0B,$08,$0A,$00
        .BYTE $9E             ; $9E = SYS
        .BYTE $32,$30,$36,$34 ; $32,$30,$36,$34 = 2064
        .BYTE $00,$00,$00,$00,$00,$00

;-----------------------------------------------------
; CopyCodeToRAM
; Copies the code into position from $0835 to $4000.
;-----------------------------------------------------
CopyCodeToRAM
        LDA #$40
        STA RAM4000HiPtr
        LDA #$08
        STA RAM0835HiPtr
        LDA #$00
        STA RAM4000LoPtr
        LDA #$35
        STA RAM0835LoPtr
        LDY #$00
        LDX #$06
b3FEF   LDA (RAM0835LoPtr),Y
        STA (RAM4000LoPtr),Y
        DEY 
        BNE b3FEF
        INC RAM4000HiPtr
        INC RAM0835HiPtr
        DEX 
        BNE b3FEF
        JMP InitializeProgram
\end{lstlisting}
\clearpage

Somehow this data is encoding the pattern that Psychedelia is drawing. It looks to be a list of X and Y 
co-ordinates with the X co-ordinates stored in \icode{starOneXPosArray} and the Y co-ordinates in
\icode{starOneYPosArray}. The only problem is they don't look much like co-ordinates. After puzzling 
for a little while I realized what's going on here. The very first pair of bytes is \icode{\$00} from
\icode{starOneXPosArray} and \icode{\$FF} from \icode{starOneYPosArray}. That can't mean that the X,Y
position is \icode{(0,255)}, so what if \icode{\$FF} means something else? What about \icode{-1}? That
would mean \icode{\$FE} means \icode{-2} and so on. Now things start to make sense. If we take 'C' as our origin the first line of 
each array can be plotted with \icode{0s} on the right hand side as follows:


\clearpage
\begin{lstlisting}
NUM_COLS = $28
NUM_ROWS = $18
;-------------------------------------------------------
; InitializeProgram
;-------------------------------------------------------
InitializeProgram   
        LDA #$00
        STA $D020    ;Border Color
        STA $D021    ;Background Color 0

        LDA #>COLOR_RAM
        STA colorRamHiPtr
        LDA #<COLOR_RAM
        STA colorRamLoPtr

        LDX #$00
b4012   LDA colorRamHiPtr
        STA colorRAMLineTableHiPtrArray,X
        LDA colorRamLoPtr
        STA colorRAMLineTableLoPtrArray,X
        CLC 
        ADC #NUM_COLS
        STA colorRamLoPtr
        LDA colorRamHiPtr
        ADC #$00
        STA colorRamHiPtr
        INX 
        CPX #NUM_ROWS+1
        BNE b4012

        JSR InitializeScreenAndText
        JMP LaunchPsychedelia

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.

\clearpage
\begin{lstlisting}
; The listing for this part is cut off so some
; of the text has to be restored.
bannerText   
        .TEXT $00,"PSYCHEDELIA...A FORETASTE BY JEFF MINTER"
.enc "none"

;-------------------------------------------------------
; InitializeScreenAndText
;-------------------------------------------------------
InitializeScreenAndText   
        JSR InitializeScreen

        LDX #NUM_COLS
b452D   LDA bannerText,X
        STA SCREEN_RAM + $03BF,X
        LDA #$0C
        STA COLOR_RAM + $03BF,X
        DEX 
        BNE b452D
        RTS 

;-------------------------------------------------------
; InitializeScreen
;-------------------------------------------------------
InitializeScreen   
        LDX #$00
b4034   LDA #$CF
        STA SCREEN_RAM + $0000,X
        STA SCREEN_RAM + $0100,X
        STA SCREEN_RAM + $0200,X
        STA SCREEN_RAM + $0300,X
        LDA #$00
        STA COLOR_RAM + $0000,X
        STA COLOR_RAM + $0100,X
        STA COLOR_RAM + $0200,X
        STA COLOR_RAM + $0300,X
        DEX 
        BNE b4034
        RTS 

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.

\clearpage
\begin{lstlisting}
;-------------------------------------------------------
; LaunchPsychedelia
;-------------------------------------------------------
LaunchPsychedelia   
        JSR ReinitializeSequences
        JSR SetUpIntteruptHandlers

MainPaintLoop   
        INC currentPositionInArrays
        LDA currentPositionInArrays
        AND MAX_INDEX_VALUE
        STA currentPositionInArrays
        TAX 
        DEC framesRemainingToNextPaintForStep,X
        BNE GoBackToStartOfLoop

        LDA initialFramesRemainingToNextPaintForStep,X
        STA framesRemainingToNextPaintForStep,X

        LDA currentColorIndexArray,X
        CMP #$FF
        BEQ GoBackToStartOfLoop

        STA currentValueInColorIndexArray
        LDA pixelXPositionArray,X
        STA pixelXPosition
        LDA pixelYPositionArray,X
        STA pixelYPosition
        JSR PaintStructureAtCurrentPosition
        LDX currentPositionInArrays
        DEC currentColorIndexArray,X
GoBackToStartOfLoop   
        JMP MainPaintLoop

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.



\clearpage
\begin{lstlisting}
;-------------------------------------------------------
; ReinitializeSequences
;-------------------------------------------------------
ReinitializeSequences   
        LDX #$00
        TXA 
b42D9   STA pixelXPositionArray,X
        STA pixelYPositionArray,X
        STA currentColorIndexArray,X
        STA initialFramesRemainingToNextPaintForStep,X
        STA framesRemainingToNextPaintForStep,X
        INX 
        CPX #$40
        BNE b42D9
        RTS 

\end{lstlisting}
\begin{lstlisting}
currentPositionInArrays   .BYTE $08 
;-------------------------------------------------------
; SetUpInterruptHandlers
;-------------------------------------------------------
SetUpIntteruptHandlers   
        SEI 
        LDA #<MainInterruptHandler
        STA $0314    ;IRQ
        LDA #>MainInterruptHandler
        STA $0315    ;IRQ

        LDA #$0A
        STA cursorXPosition
        STA cursorYPosition

        LDA #$01
        STA $D015    ;Sprite display Enable
        STA $D027    ;Sprite 0 Color
        CLI 
        RTS 

countStepsBeforeCheckingJoystickInput .BYTE $01
lastColorPainted                      .BYTE $00
\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.


\clearpage
\begin{lstlisting}
PaintStructureAtCurrentPosition   
        JSR PaintPixelForCurrentSymmetry

        LDY #$00
        LDA currentValueInColorIndexArray
        CMP #NUM_ARRAYS
        BNE CanLoopAndPaint
        RTS 

CanLoopAndPaint   
        LDA #NUM_ARRAYS
        STA countToMatchCurrentIndex
       
        LDA pixelXPosition
        STA initialPixelXPosition
        LDA pixelYPosition
        STA initialPixelYPosition

LineReadLoop   
        ; See next page for the contents of this loop.
        JMP LineReadLoop

RestorePositionsAndReturn   
        LDA initialPixelXPosition
        STA pixelXPosition
        LDA initialPixelYPosition
        STA pixelYPosition
        RTS 

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.


\clearpage
\begin{lstlisting}
LineReadLoop   
        LDA initialPixelXPosition
        CLC 
        ADC starOneXPosArray,Y
        STA pixelXPosition

        LDA initialPixelYPosition
        CLC 
        ADC starOneYPosArray,Y
        STA pixelYPosition

        TYA 
        PHA 

        JSR PaintPixelForCurrentSymmetry

        PLA 
        TAY 
        INY 

        LDA starOneXPosArray,Y
        CMP #$55
        BNE LineReadLoop

        DEC countToMatchCurrentIndex
        LDA countToMatchCurrentIndex
        CMP currentValueInColorIndexArray
        BEQ RestorePositionsAndReturn
        CMP #$01
        BEQ RestorePositionsAndReturn

        INY 
        JMP LineReadLoop

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.


\clearpage
\begin{lstlisting}
PaintPixelForCurrentSymmetry   
        ; First paint the normal pattern without any
        ; symmetry.
        LDA pixelXPosition
        PHA 
        LDA pixelYPosition
        PHA 
        JSR PaintPixel

        LDA currentSymmetrySettingForStep
        BNE HasSymmetry

CleanUpAndReturnFromSymmetry   
        PLA 
        STA pixelYPosition
        PLA 
        STA pixelXPosition
        RTS 

        RTS 

HasSymmetry   
        ; Has a pattern to paint on the Y axis
        ; symmetry so prepare for that.
        LDA #NUM_COLS
        SEC 
        SBC pixelXPosition
        STA pixelXPosition

        JSR PaintPixel

        LDA currentSymmetrySettingForStep
        CMP #$01
        BEQ CleanUpAndReturnFromSymmetry

        LDA #NUM_ROWS
        SEC 
        SBC pixelYPosition
        STA pixelYPosition
        JSR PaintPixel

        PLA 
        TAY 
        PLA 
        STA pixelXPosition
        TYA 
        PHA 
        JSR PaintPixel
        PLA 
        STA pixelYPosition
        RTS 

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.



\clearpage
\begin{lstlisting}
presetColorValuesArray
;         0    6    2    4      5    3     7      1
  .BYTE BLACK,BLUE,RED,PURPLE,GREEN,CYAN,YELLOW,WHITE
;-------------------------------------------------------
; PaintPixel
;-------------------------------------------------------
PaintPixel   
        LDA pixelXPosition
        AND #$80 ; Detect if has moved off left of screen
        BNE ReturnEarly
        LDA pixelXPosition
        CMP #NUM_COLS
        BPL ReturnEarly
        LDA pixelYPosition
        AND #$80 ; Detect if has moved off top of screen.
        BNE ReturnEarly
        LDA pixelYPosition
        CMP #NUM_ROWS
        BPL ReturnEarly

        JSR LoadXAndYPosition
        LDA (currentLineForPixelInColorRamLoPtr),Y
        AND #COLOR_MAX

        LDX #$00
b408C   CMP presetColorValuesArray,X
        BEQ b4096
        INX 
        CPX #COLOR_MAX + 1
        BNE b408C

b4096   TXA 
        STA indexOfCurrentColor
        LDX currentValueInColorIndexArray
        INX 
        CPX indexOfCurrentColor
        BEQ ActuallyPaintPixel
        BPL ActuallyPaintPixel
        RTS 

ActuallyPaintPixel   
        LDX currentValueInColorIndexArray
        LDA presetColorValuesArray,X
        STA (currentLineForPixelInColorRamLoPtr),Y
        RTS 
\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.
\begin{lstlisting}
;-------------------------------------------------------
; LoadXAndYPosition
;-------------------------------------------------------
LoadXAndYPosition   
        LDX pixelYPosition
        LDA colorRAMLineTableLoPtrArray,X
        STA currentLineForPixelInColorRamLoPtr
        LDA colorRAMLineTableHiPtrArray,X
        STA currentLineForPixelInColorRamHiPtr
        LDY pixelXPosition
ReturnEarly
        RTS 
\end{lstlisting}

\clearpage
\begin{lstlisting}
;-------------------------------------------------------
; LoadXAndYOfCursorPosition
;-------------------------------------------------------
LoadXAndYOfCursorPosition   
        LDX cursorYPosition
        LDA colorRAMLineTableLoPtrArray,X
        STA currentLineForCursorInColorRamLoPtr
        LDA colorRAMLineTableHiPtrArray,X
        STA currentLineForCursorInColorRamHiPtr
        LDY cursorXPosition
        RTS 

;-------------------------------------------------------
; PaintCursorAtCurrentPosition
;-------------------------------------------------------
PaintCursorAtCurrentPosition   
        JSR LoadXAndYOfCursorPosition
        LDA currentColorToPaint
        STA (currentLineForCursorInColorRamLoPtr),Y
        RTS 

cursorXPosition       .BYTE $1E
cursorYPosition       .BYTE $0D
indexIntoArrays       .BYTE $1A
MAX_INDEX_VALUE       .BYTE $1F
stepsSincePressedFire .BYTE $00
stepsExceeded255      .BYTE $00
smoothingDelay        .BYTE $0C

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.




\clearpage
\begin{lstlisting}
MainInterruptHandler   
        DEC countStepsBeforeCheckingJoystickInput
        BEQ b4353
        JMP RETURN_FROM_INTERRUPT

b4353   LDA #$02
        STA countStepsBeforeCheckingJoystickInput
        LDA #$00
        STA currentColorToPaint

        JSR PaintCursorAtCurrentPosition
        LDA $DC00    ;CIA1: Data Port Register A
        AND #$03
        CMP #$03
        BEQ CheckIfCursorMovedLeftOrRight

        CMP #$02
        BEQ PlayerHasPressedDown

        ; Player has pressed up. Incremeent up two lines
        ; so that when we decrement down one, we're still
        ; one up!
        INC cursorYPosition
        INC cursorYPosition

PlayerHasPressedDown   
        DEC cursorYPosition
        LDA cursorYPosition
        CMP #$FF
        BNE CheckIfCursorAtBottom

        ; Cursor has reached the top of the screen, so loop
        ; around to bottom.
        LDA #$17
        STA cursorYPosition
        JMP CheckIfCursorMovedLeftOrRight

CheckIfCursorAtBottom   
        CMP #NUM_ROWS
        BNE CheckIfCursorMovedLeftOrRight
        ; Cursor has reached the bottom of the screen, so loop
        ; around to top
        LDA #$00
        STA cursorYPosition

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.



\clearpage
\begin{lstlisting}
CheckIfCursorMovedLeftOrRight   
        LDA $DC00    ;CIA1: Data Port Register A
        AND #$0C
        CMP #$0C
        BEQ CheckIfPlayerPressedFire

        CMP #$08
        BEQ CursorMovedLeft

        ; Player has pressed right.
        INC cursorXPosition
        INC cursorXPosition
CursorMovedLeft   
        DEC cursorXPosition
        LDA cursorXPosition
        CMP #$FF
        BNE CheckIfCursorAtExtremeRight

        LDA #$27
        STA cursorXPosition
        JMP CheckIfPlayerPressedFire

CheckIfCursorAtExtremeRight   
        CMP #NUM_COLS
        BNE CheckIfPlayerPressedFire
        LDA #$00
        STA cursorXPosition

CheckIfPlayerPressedFire   
        LDA $DC00    ;CIA1: Data Port Register A
        AND #$10
        BEQ PlayerHasntPressedFire

        LDA #$00
        STA stepsSincePressedFire
        JMP DrawCursorAndReturnFromInterrupt

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.


\clearpage
\begin{lstlisting}
PlayerHasntPressedFire   
        LDA stepsExceeded255
        BEQ b43D7
        LDA stepsSincePressedFire
        BNE DrawCursorAndReturnFromInterrupt

        INC stepsSincePressedFire
b43D7   INC indexIntoArrays
        LDA indexIntoArrays
        AND MAX_INDEX_VALUE
        STA indexIntoArrays

UpdateColorIndexArray  
        TAX 
        LDA currentColorIndexArray,X
        CMP #$FF
        BNE DrawCursorAndReturnFromInterrupt

        LDA cursorXPosition
        STA pixelXPositionArray,X
        LDA cursorYPosition
        STA pixelYPositionArray,X
        LDA #COLOR_MAX
        STA currentColorIndexArray,X

        LDA smoothingDelay
        STA initialFramesRemainingToNextPaintForStep,X
        STA framesRemainingToNextPaintForStep,X

DrawCursorAndReturnFromInterrupt   
        JSR LoadXAndYOfCursorPosition
        LDA (currentLineForCursorInColorRamLoPtr),Y
        AND #COLOR_MAX
        STA lastColorPainted
        LDA #WHITE
        STA currentColorToPaint
        JSR PaintCursorAtCurrentPosition
        JMP RETURN_FROM_INTERRUPT

\end{lstlisting}
\clearpage

Populate a table of hi/lo ptrs to the color RAM
of each line on the screen (e.g. \icode(\$D800), \icode(\$D828), \icode(\$D850) etc). Each entry represents a single
line 40 bytes long and there are twenty five lines.
The last line is reserved for configuration messages.

